dnl Process this file with autoconf to produce a configure script.
AC_INIT(gopherd.h)
AM_INIT_AUTOMAKE(gofish, 1.0)
AM_CONFIG_HEADER(config.h:config.h.in)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

dnl Checks for libraries.
dnl Sun needs -lsocket
AC_CHECK_LIB(socket, socket)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h sys/time.h syslog.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_TM

dnl Checks for library functions.

dnl We cannot use this special function since it fails under FreeBSD
dnl AC_FUNC_MMAP
AC_CHECK_FUNCS(mmap)
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(gethostname select socket strdup strerror strstr strtol uname)
AC_CHECK_FUNCS(poll sendfile)

dnl Check the type of sendfile
dnl I am not completely happy with this, since
dnl Linux passes the BSD test. We are *really* only
dnl testing the header files.
if test "$ac_cv_func_sendfile" = "yes"; then
  AC_MSG_CHECKING([sendfile type])
  sendfile_type=none
  AC_TRY_LINK([
#include <sys/sendfile.h>
  ],
  [
  int out_fd, in_fd;
  off_t offset;
  size_t count;

  sendfile(out_fd, in_fd, &offset, count);
  ],
  [AC_DEFINE(HAVE_LINUX_SENDFILE) sendfile_type=Linux],
  [
  AC_TRY_LINK([
#include <sys/types.h>
#include <sys/socket.h>
#include <sys/uio.h>
  ],
  [
  int fd, s;
  off_t offset, sbytes;
  size_t nbytes;
  struct sf_hdtr *hdtr;

  sendfile(fd, s, offset, nbytes, hdtr, &sbytes, 0);
  ],
  [AC_DEFINE(HAVE_BSD_SENDFILE) sendfile_type=BSD],
  [])
  ])
  AC_MSG_RESULT([$sendfile_type])
fi

AC_MSG_CHECKING([http support])
if test "$enable_http" = "no"; then
  AC_DEFINE(USE_HTTP, 0)
else
  enable_http=yes
fi
AC_MSG_RESULT([$enable_http])

AC_OUTPUT(Makefile)
